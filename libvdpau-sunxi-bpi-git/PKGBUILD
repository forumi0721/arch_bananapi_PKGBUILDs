# Maintainer: StoneCold <forumi0721[at]gmail[dot]com>

pkgname=("libvdpau-sunxi-bananapi-git")
_gitname="libvdpau-sunxi-bananapi"
pkgver=20140727.49.e4e3ef3
pkgrel=1
pkgdesc="Experimental VDPAU for Allwinner sunxi SoCs (WiP)"
arch=("armv7h")
url="https://github.com/LeMaker/libvdpau-sunxi/"
license=("GPL")
makedepends=("git")
provides=("libvdpau-sunxi" "libvdpau-sunxi-git")
conflicts=("libvdpau-sunxi" "libvdpau-sunxi-git")
replace=("libvdpau-sunxi" "libvdpau-sunxi-git")
source=(
	"${_gitname}::git+https://github.com/LeMaker/libvdpau-sunxi.git"
	"${pkgname}.tar.xz"
)
md5sums=(
	"SKIP"
	"afc57200c8337ae6f7b12eea17b7ea82"
)

pkgver() {
	cd "${srcdir}/${_gitname}"
	echo "$(git log -1 --format="%cd" --date=short | sed "s/-//g").$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${_gitname}"
	make
}

package() {
	cd "${srcdir}/${_gitname}"
	make DESTDIR="${pkgdir}/" install

	ln -s libvdpau_sunxi.so.1 "${pkgdir}/usr/lib/vdpau/libvdpau_sunxi.so"

	install -dm755 "${pkgdir}/etc/profile.d"
	install -Dm755 "${srcdir}/${pkgname}/vdpau.sh" "${pkgdir}/etc/profile.d/"
}
