# Maintainer: StoneCold <forumi0721[at]gmail[dot]com>

pkgname=("libump-opi-git")
_gitname="libump-opi"
pkgver=20140326.15.ec06806
pkgrel=1
pkgdesc="Unified Memory Provider userspace API source code needed for xf86-video-mali compilation"
arch=("armv7h")
url="https://github.com/orangepi-xunlong/libump"
license=("GPL")
makedepends=("git")
provides=("libump" "libump-git")
conflicts=("libump" "libump-git")
replaces=("libump" "libump-git")
source=("${_gitname}::git+https://github.com/orangepi-xunlong/libump.git")
md5sums=("SKIP")

pkgver() {
	cd "${srcdir}/${_gitname}"
	echo "$(git log -1 --format="%cd" --date=short | sed "s/-//g").$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

prepare() {
	cd "${srcdir}/${_gitname}"
	autoreconf -i
}

build() {
	cd "${srcdir}/${_gitname}"
	./configure --prefix=/usr
	make
}

package() {
	cd "${srcdir}/${_gitname}"
	make DESTDIR="${pkgdir}/" install
}
