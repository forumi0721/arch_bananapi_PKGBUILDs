# Maintainer: StoneCold <forumi0721[at]gmail[dot]com>

pkgname=("libvdpau-sunxi-opipc-git")
_gitname="libvdpau-sunxi-opipc"
pkgver=20150307.66.db0b5c6
pkgrel=1
pkgdesc="Experimental VDPAU for Allwinner sunxi SoCs (WiP)"
arch=("armv7h")
url="https://github.com/orangepi-xunlong/libvdpau-sunxi"
license=("GPL")
makedepends=("git")
provides=("libvdpau-sunxi" "libvdpau-sunxi-git")
conflicts=("libvdpau-sunxi" "libvdpau-sunxi-git")
replace=("libvdpau-sunxi" "libvdpau-sunxi-git")
source=(
	"${_gitname}::git+https://github.com/orangepi-xunlong/libvdpau-sunxi.git"
	"${pkgname}.tar.xz"
)
md5sums=(
	"SKIP"
	"SKIP"
)

pkgver() {
	cd "${srcdir}/${_gitname}"
	echo "$(git log -1 --format="%cd" --date=short | sed "s/-//g").$(git rev-list --count HEAD).$(git rev-parse --short HEAD)"
}

build() {
	cd "${srcdir}/${_gitname}"
	make
}

package() {
	cd "${srcdir}/${_gitname}"
	make DESTDIR="${pkgdir}/" install

	ln -s libvdpau_sunxi.so.1 "${pkgdir}/usr/lib/vdpau/libvdpau_sunxi.so"

	install -dm755 "${pkgdir}/etc/profile.d"
	install -Dm755 "${srcdir}/${pkgname}/vdpau.sh" "${pkgdir}/etc/profile.d/"
}
